plugins {
    id 'distribution'
    id 'maven-publish'
}

def scriptsDir = file('scripts')

distributions {
    apiServer {
        distributionBaseName = 'api-server'
        def apiServerDir = project(':api-server').projectDir
        def confDir = file('conf/api-server')
        contents {
            // Include the fat JAR from hub module
            from("${apiServerDir}/target/bin") {
                include "*.jar"
                into "lib"
            }
            
            // Include only Config.toml in conf directory
            from(confDir) {
                include "Config.toml"
                into "conf"
            }

            // Include resources in conf directory
            from("${apiServerDir}/resources") {
                into "conf/resources"
            }
            
            // Include startup scripts
            from(scriptsDir) {
                include "*.sh"
                into "bin"
                fileMode = 0755
            }
            
            // Include any documentation
            from(rootProject.projectDir) {
                include "README.md"
                include "LICENSE*"
                into ""
            }
        }
    }
    
    apiServerDistribution {
        distributionBaseName = 'api-server-distribution'
        contents {
            // Include the api-server distribution zip
            from(apiServerDistZip.outputs.files) {
                into ""
            }
            
            // Include any top-level documentation
            from(rootProject.projectDir) {
                include "README.md"
                include "LICENSE*"
                into ""
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.ayeshlk'
            artifactId = 'api-server-distribution'
            artifact(tasks.getByName("apiServerDistributionDistZip"))
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ayeshLK/gradle-bal-project")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

apiServerDistZip.dependsOn(':api-server:build')
apiServerDistTar.enabled = false

apiServerDistributionDistZip.dependsOn(apiServerDistZip)
apiServerDistributionDistTar.enabled = false

build.dependsOn(apiServerDistributionDistZip)
