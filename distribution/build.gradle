plugins {
    id 'distribution'
    id 'maven-publish'
}

def scriptsDir = file('scripts')

distributions {
    apiServer {
        distributionBaseName = 'api-server'
        def apiServerDir = project(':api-server').projectDir
        def confDir = file('conf/api-server')
        contents {
            // Include the fat JAR from hub module
            from("${apiServerDir}/target/bin") {
                include "*.jar"
                into "lib"
            }
            
            // Include only Config.toml in conf directory
            from(confDir) {
                include "Config.toml"
                into "conf"
            }

            // Include resources in conf directory
            from("${apiServerDir}/resources") {
                into "conf/resources"
            }
            
            // Include startup scripts
            from(scriptsDir) {
                include "*.sh"
                into "bin"
                fileMode = 0755
            }
            
            // Include any documentation
            from(rootProject.projectDir) {
                include "README.md"
                include "LICENSE*"
                into ""
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(tasks.getByName("apiServerDistZip"))
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

apiServerDistZip.dependsOn(':api-server:build')
apiServerDistTar.enabled = false

build.dependsOn(apiServerDistZip)
