plugins {
    id "net.researchgate.release"
}

description = 'AyeshLK API Server'

allprojects {
    group = project.group
    version = project.version

    ext {
        snapshotVersion = '-SNAPSHOT'
    }
}

def moduleVersion = project.version.replace("-SNAPSHOT", "")

task verifyLocalBalVersion(type: Exec) {
    description = "Verify the local Ballerina distribution version."
    group = "Ballerina"
    commandLine BalUtils.executeBalCommand('version')
    doLast {
        def output = new ByteArrayOutputStream()
        exec {
            commandLine BalUtils.executeBalCommand('version')
            standardOutput = output
        }
        def versionOutput = output.toString().trim()
        def firstLine = versionOutput.split('\n')[0]
        def matcher = (firstLine =~ /(\d+\.\d+\.\d+)/)
        def installedVersion = ""
        if (matcher.find()) {
            installedVersion = matcher.group(1)
        }
        if (!installedVersion.equals(project.ballerinaDistributionVersion)) {
            throw new GradleException("Local Ballerina distribution version does not match project Ballerina language version. " +
                    "Expected: ${project.ballerinaDistributionVersion}, Found: ${installedVersion}")
        }
    }
}

build {
    dependsOn 'verifyLocalBalVersion'
}

release {
    buildTasks = ['build']
    failOnSnapshotDependencies = true
    versionPropertyFile = 'gradle.properties'
    tagTemplate = 'v${version}'
    git {
        requireBranch.set("release-${moduleVersion}")
        pushToRemote.set('origin')
    }
}

subprojects {
    if (project.name != 'distribution') {
        apply plugin: 'websubhub.ballerina-component'
    }
}
